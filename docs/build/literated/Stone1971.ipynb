{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "##Linear stability analysis of baroclinic instability of a 2D front based on Stone (1971)\n",
    "\n",
    "\n",
    "The resulting nondimensional, linearized Boussinesq equations of motion\n",
    "under the $f$-plane approximation are given by\n",
    "$$\n",
    "\\begin{align}\n",
    "    \\frac{D \\mathbf{u}}{Dt}\n",
    "    + \\Big(v \\frac{\\partial U}{\\partial y} + w \\frac{\\partial U}{\\partial z} \\Big) \\hat{x}\n",
    "    + \\hat{z} \\times \\mathbf{u} &=\n",
    "    -\\nabla p + \\frac{1}{\\epsilon} b \\hat{z} + E \\nabla^2 \\mathbf{u}, \\\\\n",
    "    \\frac{Db}{Dt}\n",
    "    +  v \\frac{\\partial B}{\\partial y} + w \\frac{\\partial B}{\\partial z} &= \\frac{E}{Pr} \\nabla^2 b, \\\\\n",
    "    \\nabla \\cdot \\mathbf{u} &= 0,\n",
    "\\end{align}\n",
    "$$\n",
    "where\n",
    "$$\n",
    "D/Dt \\equiv \\partial/\\partial t + U (\\partial/\\partial x)$\n",
    "$$\n",
    "is the material derivative, $\\mathbf{u} \\equiv (u, v, \\epsilon w)$ is the velocity perturbation,\n",
    "$\\epsilon=H/R$ is the aspect ratio, $p$ is the pressure perturbation, and\n",
    "$b$ is the buoyancy perturbation. The operator\n",
    "$$\n",
    "\\nabla \\equiv (\\partial/\\partial x, \\partial/\\partial y, (1/\\epsilon) \\partial/\\partial z),\n",
    "$$\n",
    "$$\n",
    "\\nabla^2 \\equiv \\partial^2/\\partial x^2 + \\partial^2/\\partial y^2 + (1/\\epsilon^2) \\partial^2/ \\partial z^2,\n",
    "$$\n",
    "where\n",
    "$$\n",
    "\\nabla_h^2 \\equiv \\partial^2 /\\partial x^2 + \\partial^2/\\partial y^2.\n",
    "$$\n",
    "\n",
    "To eliminate pressure, following [teed2010rapidly@citet, we apply the operator\n",
    "$\\hat{z} \\cdot \\nabla \\times \\nabla \\times$  and $\\hat{z} \\cdot \\nabla \\times$\n",
    "to the above momentum equation. This procedure yields governing equations of\n",
    "three perturbation variables, the vertical velocity $w$, the vertical vorticity $\\zeta \\, (=\\hat{z} \\cdot \\nabla \\times \\mathbf{u})$, and the buoyancy $b$\n",
    "$$\n",
    "\\begin{align}\n",
    "    \\frac{D}{Dt}\\nabla^2 {w}\n",
    "    + \\frac{1}{\\epsilon^2} \\frac{\\partial \\zeta}{\\partial z}\n",
    "    &= \\frac{1}{\\epsilon^2} \\nabla_h^2 b + E \\nabla^4 w,\n",
    "\\\\\n",
    "    \\frac{D \\zeta}{Dt}\n",
    "    - \\frac{\\partial U}{\\partial z}\\frac{\\partial w}{\\partial y}\n",
    "    - \\frac{\\partial w}{\\partial z} &= E \\nabla^2 \\zeta,\n",
    "\\\\\n",
    "    \\frac{Db}{Dt}\n",
    "    + v \\frac{\\partial B}{\\partial y} +\n",
    "    w \\frac{\\partial B}{\\partial z}\n",
    "    &= \\frac{E}{Pr} \\nabla^2 b,\n",
    "\\end{align}\n",
    "$$\n",
    "where\n",
    "$$\n",
    "\\nabla_h^2 \\equiv \\partial^2 /\\partial x^2 + \\partial^2/\\partial y^2$\n",
    "$$\n",
    "The benefit of using the above sets of equations is that it enables us to\n",
    "examine the instability at an along-front wavenumber $k \\to 0$.\n",
    "\n",
    "\n",
    "## Normal mode solutions\n",
    "Next we consider normal-mode perturbation solutions in the form of\n",
    "$$\n",
    "\\begin{align}\n",
    "    [w, \\zeta, b](x,y,z,t) = \\mathfrak{R}\\big([\\tilde{w}, \\, \\tilde{\\zeta}, \\, \\tilde{b}](y, z) \\, e^{i kx + \\sigma t}\\big),\n",
    "\\end{align}\n",
    "$$\n",
    "where the symbol $\\mathfrak{R}$ denotes the real part and a variable with `tilde' denotes an eigenfunction. The variable\n",
    "$\\sigma=\\sigma_r + i \\sigma_i$. The real part represents the growth rate, and the imaginary part\n",
    "shows the frequency of the  perturbation.\n",
    "\n",
    "Finally following systems of differential equations are obtained,\n",
    "$$\n",
    "\\begin{align}\n",
    "    (i k U - E \\mathcal{D}^2) \\mathcal{D}^2 \\tilde{w}\n",
    "    + \\epsilon^{-2} \\partial_z \\tilde{\\zeta}\n",
    "    - \\epsilon^{-2} \\mathcal{D}_h^2 \\tilde{b} &= -\\sigma \\mathcal{D}^2 \\tilde{w},\n",
    "\\\\\n",
    "    - \\partial_z U \\partial_y \\tilde{w}\n",
    "    - \\partial_z \\tilde{w}\n",
    "    + \\left(ik U - E \\mathcal{D}^2 \\right) \\tilde{\\zeta} &= -\\sigma \\tilde{\\zeta},\n",
    "\\\\\n",
    "    \\partial_z B \\tilde{w} + \\partial_y B  \\tilde{v} +\n",
    "    \\left[ik U - E \\mathcal{D}^2 \\right] \\tilde{b} &= -\\sigma \\tilde{b},\n",
    "\\end{align}\n",
    "$$\n",
    "where\n",
    "$$\n",
    "\\mathcal{D}^4  = (\\mathcal{D}^2 )^2 = \\big(\\partial_y^2 +\n",
    "(1/\\epsilon^2)\\partial_z^2 - k^2\\big)^2, \\,\\,\\,\\, \\text{and} \\,\\, \\mathcal{D}_h^2 = (\\partial_y^2 - k^2).\n",
    "$$\n",
    "\n",
    "We choose periodic boundary conditions in the $y$-direction and\n",
    "free-slip, rigid lid, with zero buoyancy flux in the $z$ direction, i.e.,\n",
    "$$\n",
    "\\begin{align}\n",
    "    \\tilde{w} = \\partial_{zz} \\tilde{w} =\n",
    "    \\partial_z \\tilde{\\zeta} = \\partial_z \\tilde{b} = 0,\n",
    "    \\,\\,\\,\\,\\,\\,\\, \\text{at} \\,\\,\\, {z}=0, 1.\n",
    "\\end{align}\n",
    "$$"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## load required packages"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using LazyGrids\n",
    "using LinearAlgebra\n",
    "using Printf\n",
    "using StaticArrays\n",
    "using SparseArrays\n",
    "using SparseMatrixDicts\n",
    "using FillArrays\n",
    "using SpecialFunctions\n",
    "using Parameters\n",
    "using Test\n",
    "using BenchmarkTools\n",
    "\n",
    "using JLD2\n",
    "using ModelingToolkit\n",
    "using NonlinearSolve\n",
    "\n",
    "using BiGSTARS"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Define the grid and derivative operators"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "@with_kw mutable struct TwoDimGrid{Ny, Nz}\n",
    "    y = @SVector zeros(Float64, Ny)\n",
    "    z = @SVector zeros(Float64, Nz)\n",
    "end\n",
    "nothing #hide\n",
    "\n",
    "@with_kw mutable struct ChebMarix{Ny, Nz}\n",
    "    𝒟ʸ::Array{Float64,  2}   = SparseMatrixCSC(Zeros(Ny, Ny))\n",
    "    𝒟²ʸ::Array{Float64, 2}   = SparseMatrixCSC(Zeros(Ny, Ny))\n",
    "    𝒟⁴ʸ::Array{Float64, 2}   = SparseMatrixCSC(Zeros(Ny, Ny))\n",
    "\n",
    "    𝒟ᶻ::Array{Float64,  2}   = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "    𝒟²ᶻ::Array{Float64, 2}   = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "    𝒟⁴ᶻ::Array{Float64, 2}   = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "\n",
    "    𝒟ᶻᴺ::Array{Float64,  2}  = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "    𝒟²ᶻᴺ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "    𝒟⁴ᶻᴺ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "\n",
    "    𝒟ᶻᴰ::Array{Float64,  2}  = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "    𝒟²ᶻᴰ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "    𝒟⁴ᶻᴰ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(Nz, Nz))\n",
    "end\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "## `subperscript with N' means Operator with Neumann boundary condition\n",
    "##        after kronker product\n",
    "##    `subperscript with D' means Operator with Dirchilet boundary condition\n",
    "##        after kronker product"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "@with_kw mutable struct Operator{N}\n",
    "    𝒟ʸ::Array{Float64,  2}   = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟²ʸ::Array{Float64, 2}   = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟⁴ʸ::Array{Float64, 2}   = SparseMatrixCSC(Zeros(N, N))\n",
    "\n",
    "    𝒟ᶻ::Array{Float64,  2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟²ᶻ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "\n",
    "    𝒟ᶻᴺ::Array{Float64,  2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟²ᶻᴺ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟⁴ᶻᴺ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "\n",
    "    𝒟ᶻᴰ::Array{Float64,  2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟ʸᶻᴰ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟²ᶻᴰ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟⁴ᶻᴰ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "\n",
    "    𝒟ʸ²ᶻᴰ::Array{Float64,  2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "    𝒟²ʸ²ᶻᴰ::Array{Float64, 2}  = SparseMatrixCSC(Zeros(N, N))\n",
    "end\n",
    "nothing #hide\n",
    "\n",
    "@with_kw mutable struct MeanFlow{N}\n",
    "    B₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "    U₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "\n",
    "  ∇ʸU₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "  ∇ᶻU₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "  ∇ʸB₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "  ∇ᶻB₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "\n",
    "  ∇ʸʸU₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "  ∇ᶻᶻU₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "  ∇ʸᶻU₀::Array{Float64, 2} = SparseMatrixCSC(Zeros(N, N))\n",
    "end\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Constructing the derivative operators"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function construct_matrices(Op, mf, grid, params)\n",
    "    Y, Z = ndgrid(grid.y, grid.z)\n",
    "    Y    = transpose(Y)\n",
    "    Z    = transpose(Z)\n",
    "\n",
    "    # basic state\n",
    "    B₀   = @. 1.0/params.Γ * Z - Y\n",
    "    ∂ʸB₀ = - 1.0 .* ones(size(Y))\n",
    "    ∂ᶻB₀ = 1.0/params.Γ .* ones(size(Y))\n",
    "\n",
    "    U₀      = @. 1.0 * Z - 0.5params.H\n",
    "    ∂ᶻU₀    = ones( size(Y))\n",
    "    ∂ʸU₀    = zeros(size(Y))\n",
    "\n",
    "    ∂ʸʸU₀   = zeros(size(Y))\n",
    "    ∂ʸᶻU₀   = zeros(size(Y))\n",
    "    ∂ᶻᶻU₀   = zeros(size(Y))\n",
    "\n",
    "      B₀  = B₀[:];\n",
    "      U₀  = U₀[:];\n",
    "    ∂ʸB₀  = ∂ʸB₀[:];\n",
    "    ∂ᶻB₀  = ∂ᶻB₀[:];\n",
    "\n",
    "    ∂ᶻU₀  = ∂ᶻU₀[:];\n",
    "    ∂ʸU₀  = ∂ʸU₀[:];\n",
    "\n",
    "    ∂ʸʸU₀ = ∂ʸʸU₀[:];\n",
    "    ∂ʸᶻU₀ = ∂ʸᶻU₀[:];\n",
    "    ∂ᶻᶻU₀ = ∂ᶻᶻU₀[:];\n",
    "\n",
    "    mf.B₀[diagind(mf.B₀)] = B₀\n",
    "    mf.U₀[diagind(mf.U₀)] = U₀\n",
    "\n",
    "    mf.∇ᶻU₀[diagind(mf.∇ᶻU₀)] = ∂ᶻU₀\n",
    "    mf.∇ʸU₀[diagind(mf.∇ʸU₀)] = ∂ʸU₀\n",
    "\n",
    "    mf.∇ʸB₀[diagind(mf.∇ʸB₀)] = ∂ʸB₀\n",
    "    mf.∇ᶻB₀[diagind(mf.∇ᶻB₀)] = ∂ᶻB₀\n",
    "\n",
    "    mf.∇ʸʸU₀[diagind(mf.∇ʸʸU₀)] = ∂ʸʸU₀;\n",
    "    mf.∇ᶻᶻU₀[diagind(mf.∇ᶻᶻU₀)] = ∂ᶻᶻU₀;\n",
    "    mf.∇ʸᶻU₀[diagind(mf.∇ʸᶻU₀)] = ∂ʸᶻU₀;\n",
    "\n",
    "    N  = params.Ny * params.Nz\n",
    "    I⁰ = sparse(Matrix(1.0I, N, N)) #Eye{Float64}(N)\n",
    "    s₁ = size(I⁰, 1); s₂ = size(I⁰, 2)\n",
    "\n",
    "    # allocating memory for the LHS and RHS matrices\n",
    "    𝓛₁ = SparseMatrixCSC(Zeros{ComplexF64}(s₁, 3s₂))\n",
    "    𝓛₂ = SparseMatrixCSC(Zeros{ComplexF64}(s₁, 3s₂))\n",
    "    𝓛₃ = SparseMatrixCSC(Zeros{ComplexF64}(s₁, 3s₂))\n",
    "\n",
    "    ℳ₁ = SparseMatrixCSC(Zeros{Float64}(s₁, 3s₂))\n",
    "    ℳ₂ = SparseMatrixCSC(Zeros{Float64}(s₁, 3s₂))\n",
    "    ℳ₃ = SparseMatrixCSC(Zeros{Float64}(s₁, 3s₂))\n",
    "\n",
    "    ∇ₕ² = SparseMatrixCSC(Zeros(N, N))\n",
    "    H   = SparseMatrixCSC(Zeros(N, N))\n",
    "\n",
    "    ∇ₕ² = (1.0 * Op.𝒟²ʸ - 1.0 * params.kₓ^2 * I⁰)\n",
    "\n",
    "\n",
    "    H = inverse_Lap_hor(∇ₕ²)\n",
    "    @assert norm(∇ₕ² * H - I⁰) ≤ 1.0e-4 \"difference in L2-norm should be small\"\n",
    "\n",
    "\n",
    "    D⁴  = (1.0 * Op.𝒟⁴ʸ\n",
    "        + 1.0/params.ε^4 * Op.𝒟⁴ᶻᴰ\n",
    "        + 1.0params.kₓ^4 * I⁰\n",
    "        - 2.0params.kₓ^2 * Op.𝒟²ʸ\n",
    "        - 2.0/params.ε^2 * params.kₓ^2 * Op.𝒟²ᶻᴰ\n",
    "        + 2.0/params.ε^2 * Op.𝒟²ʸ²ᶻᴰ)\n",
    "\n",
    "    D²  = (1.0/params.ε^2 * Op.𝒟²ᶻᴰ + 1.0 * ∇ₕ²)\n",
    "    Dₙ² = (1.0/params.ε^2 * Op.𝒟²ᶻᴺ + 1.0 * ∇ₕ²)\n",
    "\n",
    "    # 1. uᶻ (vertical velocity)  equation (bcs: uᶻ = ∂ᶻᶻuᶻ = 0 @ z = 0, 1)\n",
    "    𝓛₁[:,    1:1s₂] = (-1.0params.E * D⁴\n",
    "                    + 1.0im * params.kₓ * mf.U₀ * D²) * params.ε^2\n",
    "    𝓛₁[:,1s₂+1:2s₂] = 1.0 * Op.𝒟ᶻᴺ\n",
    "    𝓛₁[:,2s₂+1:3s₂] = -1.0 * ∇ₕ²\n",
    "\n",
    "    # 2. ωᶻ (vertical vorticity) equation (bcs: ∂ᶻωᶻ = 0 @ z = 0, 1)\n",
    "    𝓛₂[:,    1:1s₂] = - 1.0 * mf.∇ᶻU₀ * Op.𝒟ʸ - 1.0 * Op.𝒟ᶻᴰ\n",
    "    𝓛₂[:,1s₂+1:2s₂] = (1.0im * params.kₓ * mf.U₀ * I⁰\n",
    "                    - 1.0params.E * Dₙ²)\n",
    "    𝓛₂[:,2s₂+1:3s₂] = 0.0 * I⁰\n",
    "\n",
    "    # 3. b (buoyancy) equation (bcs: b = 0 @ z = 0, 1)\n",
    "    𝓛₃[:,    1:1s₂] = (1.0 * mf.∇ᶻB₀ * I⁰\n",
    "                    - 1.0 * mf.∇ʸB₀ * H * Op.𝒟ʸᶻᴰ)\n",
    "    𝓛₃[:,1s₂+1:2s₂] = 1.0im * params.kₓ * mf.∇ʸB₀ * H * I⁰\n",
    "    𝓛₃[:,2s₂+1:3s₂] = (-1.0params.E * Dₙ²\n",
    "                    + 1.0im * params.kₓ * mf.U₀ * I⁰)\n",
    "\n",
    "    𝓛 = ([𝓛₁; 𝓛₂; 𝓛₃]);\n",
    "\n",
    "\n",
    "    cnst = -1.0\n",
    "    ℳ₁[:,    1:1s₂] = 1.0cnst * params.ε^2 * D²;\n",
    "    ℳ₂[:,1s₂+1:2s₂] = 1.0cnst * I⁰;\n",
    "    ℳ₃[:,2s₂+1:3s₂] = 1.0cnst * I⁰;\n",
    "    ℳ = ([ℳ₁; ℳ₂; ℳ₃])\n",
    "\n",
    "    return 𝓛, ℳ\n",
    "end\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 4
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Define the parameters"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "@with_kw mutable struct Params{T<:Real} @deftype T\n",
    "    L::T        = 1.0        # horizontal domain size\n",
    "    H::T        = 1.0        # vertical domain size\n",
    "    Γ::T        = 0.1        # front strength Γ ≡ M²/f² = λ/H = 1/ε → ε = 1/Γ\n",
    "    ε::T        = 0.1        # aspect ratio ε ≡ H/L\n",
    "    kₓ::T       = 0.0        # x-wavenumber\n",
    "    E::T        = 1.0e-9     # Ekman number\n",
    "    Ny::Int64   = 48         # no. of y-grid points\n",
    "    Nz::Int64   = 24         # no. of z-grid points\n",
    "    method::String = \"krylov\"\n",
    "end\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 5
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Define the eigenvalue solver"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function EigSolver(Op, mf, grid, params, σ₀)\n",
    "\n",
    "    𝓛, ℳ = construct_matrices(Op, mf, grid, params)\n",
    "\n",
    "    N = params.Ny * params.Nz\n",
    "    MatrixSize = 3N\n",
    "    @assert size(𝓛, 1)  == MatrixSize &&\n",
    "            size(𝓛, 2)  == MatrixSize &&\n",
    "            size(ℳ, 1)  == MatrixSize &&\n",
    "            size(ℳ, 2)  == MatrixSize \"matrix size does not match!\"\n",
    "\n",
    "    if params.method == \"shift_invert\"\n",
    "        λₛ = EigSolver_shift_invert( 𝓛, ℳ, σ₀=σ₀)\n",
    "\n",
    "    elseif params.method == \"krylov\"\n",
    "\n",
    "        λₛ, Χ = EigSolver_shift_invert_krylov( 𝓛, ℳ, σ₀=σ₀, maxiter=40, which=:LR)\n",
    "\n",
    "\n",
    "    elseif params.method == \"arnoldi\"\n",
    "\n",
    "        λₛ, Χ = EigSolver_shift_invert_arnoldi( 𝓛, ℳ, σ₀=σ₀, maxiter=40, which=:LR)\n",
    "    end\n",
    "    # ======================================================================\n",
    "    @assert length(λₛ) > 0 \"No eigenvalue(s) found!\"\n",
    "\n",
    "    @printf \"||𝓛Χ - λₛℳΧ||₂: %f \\n\" norm(𝓛 * Χ[:,1] - λₛ[1] * ℳ * Χ[:,1])\n",
    "\n",
    "    @printf \"largest growth rate : %1.4e%+1.4eim\\n\" real(λₛ[1]) imag(λₛ[1])\n",
    "\n",
    "    return λₛ[1] #, Χ[:,1]\n",
    "end\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 6
  },
  {
   "cell_type": "markdown",
   "source": [
    "## solving the Stone problem"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "sigma: 0.011500 \n",
      "(3456,)\n",
      "found eigenvalue: 0.028452 + im 0.000000 \n",
      "||𝓛Χ - λₛℳΧ||₂: 0.000697 \n",
      "largest growth rate : 2.8452e-02+2.4014e-10im\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "function solve_Stone1971(kₓ::Float64=0.0)\n",
    "    params      = Params{Float64}(kₓ=0.5)\n",
    "    grid        = TwoDimGrid{params.Ny,  params.Nz}()\n",
    "    diffMatrix  = ChebMarix{ params.Ny,  params.Nz}()\n",
    "    Op          = Operator{params.Ny * params.Nz}()\n",
    "    mf          = MeanFlow{params.Ny * params.Nz}()\n",
    "\n",
    "    Construct_DerivativeOperator!(diffMatrix, grid, params)\n",
    "    ImplementBCs_cheb!(Op, diffMatrix, params)\n",
    "\n",
    "    σ₀   = 0.01\n",
    "    params.kₓ = kₓ\n",
    "\n",
    "    λₛ = EigSolver(Op, mf, grid, params, σ₀)\n",
    "\n",
    "    # Analytical solution of Stone (1971) for the growth rate\n",
    "    cnst = 1.0 + 1.0/params.Γ + 5.0*params.ε^2 * params.kₓ^2/42.0\n",
    "    λₛₜ = 1.0/(2.0*√3.0) * (params.kₓ - 2.0/15.0 * params.kₓ^3 * cnst)\n",
    "\n",
    "    return abs(λₛ.re - λₛₜ) < 1e-3\n",
    "\n",
    "end\n",
    "nothing #hide\n",
    "\n",
    "solve_Stone1971(0.1)\n",
    "nothing #hide"
   ],
   "metadata": {},
   "execution_count": 7
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  },
  "kernelspec": {
   "name": "julia-1.11",
   "display_name": "Julia 1.11.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}
